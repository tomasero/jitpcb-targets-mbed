# Modifies the environment to build mbed firmware for this target
Import('env')

env['MBED_TARGETS_JSON_FILE'] = File('mbed/hal/targets.json').srcnode()

SConscript('mbed-scons/SConscript-env-gcc-arm', exports='env', duplicate=0)
SConscript('mbed-scons/SConscript-mbed', exports='env', duplicate=0)

SConscript('mbed-scons/targets/SConscript-mbed-env-stm32l432kc', exports='env', duplicate=0)

mbed_lib = env.MbedLikeLibrary(
    'mbed', 'mbed/hal/',
    ['api/', 'common/', 'hal/', 'targets/cmsis/', 'targets/hal/'],
    ignore_sources=[
      'targets/cmsis/TARGET_STM/TARGET_STM32L4/TARGET_NUCLEO_L432KC/cmsis_nvic.c',
    ],
    additional_sources=[
      'mbed-overrides/nucleo-l432kc-bl/cmsis_nvic.c'
    ]
)

env.Prepend(LIBS=mbed_lib)

env['MBED_LINKSCRIPT'] = File('mbed-overrides/nucleo-l432kc-bl/STM32L432XX.ld').srcnode()

env.Append(LINKFLAGS=[
  '-Wl,--whole-archive',  # used to compile mbed HAL, which uses funky weak symbols
  mbed_lib,
  '-Wl,--no-whole-archive',
  '--specs=nosys.specs',
])

env.Append(CPPPATH=Dir('targets/nucleo-l432kc-bl').srcnode())
